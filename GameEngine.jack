// Game Engine
// Handles game mechanics

class GameEngine {

  field Settings settings;
  field PXEngine px;
  field GXEngine gx;
  field Array bulletList;
  field int powerSetting;
  field int deBounce; // counts how many frames since the last keystroke

  // working variables for methods. Defined here
  // to only make one copy

  constructor GameEngine new(Settings gameSettings,
                             GXEngine myGXEngine,
                             PXEngine myPXEngine) {

    var int i;
    let settings = gameSettings;
    let bulletList = Array.new(settings.getMaxBullets());
    // initialize bulletList
    let i=0;
    while (i<settings.getMaxBullets()) {
      let bulletList[i] = 0;
      let i=i+1; }

    let gx = myGXEngine;
    let px = myPXEngine;
    let powerSetting = 20;
    let deBounce = 0;
    return this;
  }


  method void takeTurn(Player player) {
    // Allows a player to take a turn.
    // Returns when either dead or next turn
      var int action;
      var int foo, bar;
      var Attributes currentAttributes;

      let currentAttributes = player.getSpriteAttributes();
      let foo = settings.getPlayerStartingPower();
      do player.setPowerRemaining(foo);
      do updateScreen(true);

      while (~player.getIsDead() &
             (player.getPowerRemaining()>0) ){
        let action = __getInput__();

        if ( (action=1) & (deBounce<1) ) {
          let foo = currentAttributes.getRotation();
          let bar = settings.getthrusterAcceleration();
          do px.applyAcceleration(currentAttributes,
                               powerSetting*bar, foo);
        }
        if (action=3) {
          let foo=settings.getRotationSensativity()*-1;
          do px.applyRotation(currentAttributes, foo);
        }
        if (action=4) {
          let foo=settings.getRotationSensativity();
          do px.applyRotation(currentAttributes, foo);
        }
        if (action=5) { // increase power
          let foo=settings.getPowerSensativity();
          do changePower(foo);
        }
        if (action=6) { // decrease power
          let foo=settings.getPowerSensativity();
          do changePower(foo*-1);
        }
        if ( (action=7) & (deBounce<1) ) { //shoot
          do __shoot__(currentAttributes);
        }
        // reset the debounce counter.
        if (~(action=0)) {let deBounce=settings.getDebounceSetting();}
        if (deBounce > 0){ let deBounce = deBounce-1; }

        let foo = settings.getTimeStep();
        do px.stepForward(foo);
        do updateScreen(false);
        do Sys.wait(foo);

        // detect collisions
        do checkForCollisions();

        // detect player colision
        let currentAttributes = player.getSpriteAttributes();
        let foo = px.detectCollision(currentAttributes);
        if ( ~(foo=0) ) { do player.kill(); do Output.printInt(foo);}
      }

      return;
    }

    method void __shoot__(Attributes playerAttributes) {
      // shoots a bullet from player. playerAttributes
      // is the attributes object from the shooting player
      var Bullet newBullet;
      var Attributes currentAttributes;
      var int Px,Py,radius, accel, rotation,mass;

      let radius=settings.getBulletRadius();
      let accel=powerSetting;
      let Px = playerAttributes.getPx();
      let Py = playerAttributes.getPy();
      let mass=settings.getBulletMass();

      let newBullet = Bullet.new(Px,Py,0,0,radius, mass);
      do registerBullet(newBullet);
      do gx.registerBullet(newBullet);
      do px.registerBullet(newBullet);
      if ( newBullet.getIsDead() ) {
        do newBullet.dispose();
      } else {
        let currentAttributes = newBullet.getAttributes();
        let rotation = playerAttributes.getRotation();

        do px.applyAcceleration(currentAttributes,accel, rotation);
      }
      return;
    }

    method void changePower(int powerDelta) {
      let powerSetting = powerSetting + powerDelta;
      do gx.drawPowerIndicator(powerSetting);
      return;
    }


    method int __getInput__() {
      // Polls the keyboard for input and look up
      // the action in the keybinds. Returns an
      // int coresponding with that action.
      var char inputKey;
      var char action;

      let inputKey = Keyboard.keyPressed();
      //do Output.moveCursor(0,0);
      //do Output.printInt(inputKey);
      // don't register inputs if a recent input was made.
      let action = settings.getDesiredAction(inputKey);
      return action;
    }

    method void updateScreen(bool forceRedraw) {
    // if force redraw is true, it'll blank the screen and
    // redraw everything
      if (forceRedraw) {
        do Screen.clearScreen();
        do gx.drawScreenEdges();
        do gx.drawPowerIndicator(powerSetting);
      }
      do gx.drawAllObjects(forceRedraw);
      return;
    }

    method void killBullet(Bullet killBullet) {
      do unregisterBullet(killBullet);
      do gx.unregisterBullet(killBullet);
      do px.unregisterBullet(killBullet);
      do killBullet.dispose();
      do updateScreen(true);
      return;
  }

  method void checkForCollisions() {
    var int i, collisionCode, activationAge;
    var Bullet fooBullet;
    var Attributes currentAttributes;

    // check bullet collisions
    let i = 0;
    while(~(bulletList[i]=0) & (i<settings.getMaxBullets()) ) {
      let fooBullet = bulletList[i];
      do fooBullet.agePlusOne();
      let activationAge = settings.getBulletActivationAge();
      if (fooBullet.getAge()>activationAge) { // only check for collisions if the bullet is active
        let currentAttributes = fooBullet.getAttributes();
        let collisionCode = px.detectCollision(currentAttributes);
        if (~(collisionCode=0)) {do killBullet(fooBullet);}
      }
      let i=i+1;
    }
    return;
  }


  method void registerBullet(Bullet newBullet) {
    // adds an gravitySink to the list
    var int i;
    let i = 0;
    while(~(bulletList[i]=0) & (i<settings.getMaxBullets()) ) {
      let i=i+1;
    }
    // if we've reached the max number if bullets, go ahead and kill this one.
    if (i > (settings.getMaxBullets()-1)) {
      do newBullet.setIsDead(true);
    } else {
    let bulletList[i] = newBullet;
    }
    return;
  }

  method void unregisterBullet(Bullet killBullet) {
    var int i;
    let i = 0;
    while(~(bulletList[i]=killBullet) & ~(bulletList[i]=0) & (i<settings.getMaxBullets()) ) {
      let i=i+1;
    }
    if (bulletList[i]=killBullet) { let bulletList[i]=0;}
    return;
  }

}
