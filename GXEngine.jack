// Graphics Engine
// Destorys all drawable objects when destroyed.

class GXEngine {

  field Settings settings;
  field ObjectManager OM;
  field AdvancedMath mathEngine;

  field int resX, resY, screenSizeX, screenSizeY;
  field int screenLeftX, screenRightX;
  field int screenTopY, screenBottomY;

  constructor GXEngine new(Settings gameSettings, AdvancedMath newMX, ObjectManager newOM) {
    var int i;

    let settings=gameSettings;
    let mathEngine = newMX;
    let OM = newOM;
    let resX = settings.getworldResolutionX();
    let resY = settings.getworldResolutionY();
    let screenSizeX = settings.getscreenSizeX();
    let screenSizeY = settings.getscreenSizeY();

    let screenLeftX = settings.getScreenBoundLeft();
    let screenRightX = settings.getScreenBoundRight();
    let screenTopY=settings.getScreenBoundTop();
    let screenBottomY=settings.getScreenBoundBottom();

    return this;
  }

  method void drawAllObjects(bool forceDraw) {
    // draws all Sprites that needs updating.
    // if forceDraw=true, it'll draw everything regardless.
    var int i;
    var Sprite spriteSpecimen;
    var Attributes attributeSpecimen;

    let i = 0;
    while(~(OM.getAttributes(i) = 0)) {
      let attributeSpecimen=OM.getAttributes(i);

      if (attributeSpecimen.getNeedsUpdate() | forceDraw){ // redraw sprite if it needs to be redrawn.
        do draw(attributeSpecimen, false); // erase
        do draw(attributeSpecimen, true); // erase
        do attributeSpecimen.setNeedsUpdate(false);
      }
      let i=i+1;
    }

    return;
  }

  method void draw(Attributes att, bool drawOrErase) {
    // draws the parent object to the screen.
    if(att.getType()=1) {do drawPlayerSprite(att, drawOrErase);}
    if(att.getType()=2) {do drawGravitySink(att, drawOrErase);}
    if(att.getType()=3) {do drawBullet(att, drawOrErase);}

    return;
    }

  method void drawPlayerSprite(Attributes att, bool drawOrErase) {
    // draws a single PlayerSprite
    var int x,y,rot;
    var int angle, length;
    var int x1,y1, x2,y2, x3,y3; // 1-front, 2-back left, 3-back right
    var int resX, resY;

    if (att.getIsDead()){return;} // if this player is dead, remove him from the screen.

    let angle = settings.getPlayerSpritePointAngle();
    let length = settings.getPlayerSpriteSideLength();
    let resX = settings.getworldResolutionX();
    let resY = settings.getworldResolutionY();


    if (drawOrErase) {
      let x = att.getPx()/resX + settings.getScreenBoundLeft(); // we're dividing by resolution to
      let y = att.getPy()/resY + settings.getScreenBoundTop(); // convert from real space to pixel space
      let rot = att.getRotation();
    } else {
      // if we're erasing we need to use the old attributes
      let x = att.getOldPx()/resX + settings.getScreenBoundLeft();
      let y = att.getOldPy()/resY + settings.getScreenBoundTop();
      let rot = att.getOldRotation();
    }

    let x1=x;
    let y1=y;

    let x1=x+((mathEngine.sin100(rot)*(length/2))/100);
    let y1=y-((mathEngine.cos100(rot)*(length/2))/100);

    let x2=x1-((mathEngine.sin100(rot-angle)*length)/100);
    let y2=y1+((mathEngine.cos100(rot-angle)*length)/100);

    let x3=x1-((mathEngine.sin100(rot+angle)*length)/100);
    let y3=y1+((mathEngine.cos100(rot+angle)*length)/100);

    do Screen.setColor(drawOrErase);
    do Screen.drawLine(x1, y1, x2, y2);
    do Screen.drawLine(x2, y2, x3, y3);
    do Screen.drawLine(x3, y3, x1, y1);
   return;
  }

  method void drawBullet(Attributes att, bool drawOrErase) {
    // draws a single bullet
    var int x,y,r;
    var int resX, resY;

    let resX = settings.getworldResolutionX();
    let resY = settings.getworldResolutionY();

    if (drawOrErase) {
      let x = att.getPx()/resX + settings.getScreenBoundLeft(); // we're dividing by resolution to
      let y = att.getPy()/resY + settings.getScreenBoundTop(); // convert from real space to pixel space
      let r = att.getRadius();
    } else {
      // if we're erasing we need to use the old attributes
      let x = att.getOldPx()/resX + settings.getScreenBoundLeft();
      let y = att.getOldPy()/resY + settings.getScreenBoundTop();
      let r = att.getOldRadius();
    }
    do Screen.setColor(drawOrErase);
    do Screen.drawCircle(x, y, r);
   return;
  }

  method void drawGravitySink(Attributes att, bool drawOrErase) {
    // draws a single gravitySink
    var int x,y,r;
    var int resX, resY;

    let resX = settings.getworldResolutionX();
    let resY = settings.getworldResolutionY();

    let x = att.getPx()/resX + settings.getScreenBoundLeft(); // convert from real space to
    let y = att.getPy()/resY + settings.getScreenBoundTop(); // pixel space
    let r = att.getRadius();

    do Screen.setColor(drawOrErase);
    do Screen.drawCircle(x, y, r);
   return;
  }

  method void drawScreenEdges() {
    do Screen.setColor(true);
    do Screen.drawLine(screenLeftX, screenTopY, screenRightX, screenTopY);
    do Screen.drawLine(screenRightX, screenTopY, screenRightX, screenBottomY);
    do Screen.drawLine(screenRightX, screenBottomY, screenLeftX, screenBottomY);
    do Screen.drawLine(screenLeftX, screenBottomY, screenLeftX, screenTopY);
    return;
  }

  method void drawInstructions() {
    var int upperLeftX, upperLeftY;

    let upperLeftX = 42;
    let upperLeftY = 5;

    do Output.moveCursor(upperLeftY, upperLeftX);
    do Output.printString("Left/Right:");
    do Output.moveCursor(upperLeftY + 1, upperLeftX);
    do Output.printString("  Rotate Ship");
    do Output.moveCursor(upperLeftY + 3, upperLeftX);
    do Output.printString("Up/Down:");
    do Output.moveCursor(upperLeftY + 4, upperLeftX);
    do Output.printString("  Adjust Power");
    do Output.moveCursor(upperLeftY + 6, upperLeftX);
    do Output.printString("Space Bar:");
    do Output.moveCursor(upperLeftY + 7, upperLeftX);
    do Output.printString("  Shoot EnergyBomb(TM)");
    do Output.moveCursor(upperLeftY + 9, upperLeftX);
    do Output.printString("J:");
    do Output.moveCursor(upperLeftY + 10, upperLeftX);
    do Output.printString("  Execute HyperJump");



    return;
  }

  method void drawPowerIndicator(int maxPower, int powerRemaining,int powerSetting) {
    var int x,y,height, width, carrotWidth, carrotHalfHight;
    var int powerIndicatorRatio; // ratio between the power indicator height
                             // and the max amount of power
    let x=10;
    let y=60;
    let height=100;
    let width=10;
    let carrotWidth=3;
    let carrotHalfHight = 3;


    let powerIndicatorRatio = maxPower/height;

    // erase previous power indicator
    do Screen.setColor(false);
    do Screen.drawRectangle(x,y, x+width+carrotWidth,y+height);

    do Screen.setColor(true);
    // draw power remaining indicator
    do Screen.drawRectangle(x+2,y+((maxPower - powerRemaining)/powerIndicatorRatio),
                            x+width-4,y+height);
    // draw power remaining outline
    do Screen.drawLine(x+1,y-1,x+1,y+height);
    do Screen.drawLine(x+width-3,y-1,x+width-3,y+height);
    do Screen.drawLine(x+1,y-1,x-1,y-1);
    do Screen.drawLine(x+width-3,y-1,x+width-1,y-1);

    // draw power setting carrot
    do Screen.drawLine(x+width,  y+height-(powerSetting/powerIndicatorRatio),
                    x+width+carrotWidth, y+height-(powerSetting/powerIndicatorRatio)-carrotHalfHight );
    do Screen.drawLine(x+width,  y+height-(powerSetting/powerIndicatorRatio),
                    x+width+carrotWidth, y+height-(powerSetting/powerIndicatorRatio)+carrotHalfHight );

    return;
  }

  method void drawExplosion(int x, int y, int size) {
    // Draws a cool exposion to the screen
    do Screen.setColor(true);
    do Screen.drawCircle(x+(size/2),y,size/2);
    do Screen.drawCircle(x,y+(size/2),size/2);
    do Screen.setColor(false);
    do Screen.drawCircle(x+(size/3),y,size/2);
    do Screen.drawCircle(x,y+(size/3),size/2);
    do Screen.setColor(true);
    do Screen.drawCircle(x-(size/2),y,size/2);
    do Screen.drawCircle(x,y+(size/2),size/2);
    do Screen.setColor(false);
    do Screen.drawCircle(x+(size/3),y,size/2);
    do Screen.drawCircle(x,y-(size/3),size/2);
    do Sys.wait(100);
    do Screen.setColor(false);
    do Screen.drawCircle(x-(size/3),y,size/2);
    do Screen.drawCircle(x,y+(size/2),size/2);
    do Screen.setColor(true);
    do Screen.drawCircle(x+(size/3),y,size/2);
    do Screen.drawCircle(x,y-(size/2),size/2);
    do Screen.setColor(false);
    do Screen.drawCircle(x+(size/3),y,size/2);
    do Screen.drawCircle(x,y+(size/2),size/2);
    do Screen.setColor(true);
    do Screen.drawCircle(x+(size/3),y,size/2);
    do Screen.drawCircle(x,y-(size/4),size/2);
    do Sys.wait(100);
    return;
  }

  method void displayGameResult(string winner) {
    // displays the winner of a game.
    // 0-Draw, 1-Player 1 wins 2-Player 2 wins
    do Screen.setColor(true);
    do Screen.drawRectangle(settings.getBannerLeftX(),110,settings.getBannerRightX(),122);
    do Output.moveCursor(10,8);
    do Output.printString(winner);
    do Output.printString(" wins! Congratulations ");
    do Output.printString(winner);
    do Output.printString("!");


    return;
  }

  method void doLoadingScreen() {
    // displays the winner of a game.
    // 0-Draw, 1-Player 1 wins 2-Player 2 wins
    do Screen.setColor(true);
    do Screen.drawRectangle(settings.getBannerLeftX(),110,settings.getBannerRightX(),122);
    do Output.moveCursor(10,8);
    do Output.printString("Smashing Atoms... Please Wait...");
    return;
  }

  method void doIntro() {

    var int titleX, titleY;
    var int menuX, menuY;
    var int currentSelection, finalSelection;
    var int carrotX, carrotY, carrotY0;
    var int carrotHalfHight, carrotWidth;
    var int numberOfSelections;
    var int distanceBetweenItems;
    var char selectionKey;
    var char upKey, downKey, leftKey, rightKey;
    var char carrot, spaceChar, enterChar;
    var int randomSeed;
    var int difficultySetting;
    var Array difficultySettingNames;

    let titleX = 20;
    let titleY = 2;
    let menuX = 10;
    let menuY = 5;
    let distanceBetweenItems=2;
    let numberOfSelections = 3;
    let upKey = 131;
    let downKey = 133;
    let leftKey = 130;
    let rightKey = 132;
    let carrot = 62;
    let spaceChar=32;
    let enterChar = 128;
    let difficultySetting=1;
    let difficultySettingNames=Array.new(3);
    let difficultySettingNames[0]="Zero Entropy         ";
    let difficultySettingNames[1]="Not-so-distant future";
    let difficultySettingNames[2]="Big Bang             ";

    let carrotX = 20;
    let finalSelection=0;
    let currentSelection =1;

    // print title
    do Output.moveCursor(titleY, titleX);
    do Output.printString("GRAVITY WARS!");


    // print menu
    do Output.moveCursor(menuY,menuX);
    do Output.printString("Engage in a GRAVITY WAR");
    do Output.moveCursor(menuY+distanceBetweenItems,menuX);
    do Output.printString("Number of PLAYERS: ");
    do Output.printInt(settings.getNumberOfPlayers());
    do Output.moveCursor(menuY+(distanceBetweenItems*2),menuX);
    do Output.printString("Difficulty: ");
    do Output.printString(difficultySettingNames[difficultySetting]);


    // Print cursor
    do Output.moveCursor(currentSelection-1+menuY,menuX-1);
    do Output.printChar(carrot);

    while (finalSelection=0){
      let selectionKey=Keyboard.keyPressed();

      // Start Keyboard Handling
      if ( ~(selectionKey=0) ) {
        do Output.moveCursor((currentSelection-1)*distanceBetweenItems+menuY,menuX-1);
        do Output.printChar(spaceChar);
      }
      // up/down
      if ( (selectionKey=upKey) & (currentSelection>1) ){let currentSelection = currentSelection-1; }
      if ( (selectionKey=downKey) & (currentSelection<numberOfSelections) ){let currentSelection = currentSelection+1;}
      // change # of players
      if ( (selectionKey=rightKey) & (settings.getNumberOfPlayers()<settings.getMaxPlayers()) & (currentSelection=2) )
        {do settings.setNumberOfPlayers(settings.getNumberOfPlayers()+1);}
      if ( (selectionKey=leftKey) & (settings.getNumberOfPlayers()>0) & (currentSelection=2) )
        {do settings.setNumberOfPlayers(settings.getNumberOfPlayers()-1);}
      // change difficulty settings
      if ( (selectionKey=rightKey) & (difficultySetting<2) & (currentSelection=3) )
        {let difficultySetting = difficultySetting+1;}
      if ( (selectionKey=leftKey) & (difficultySetting>0) & (currentSelection=3) )
        {let difficultySetting = difficultySetting-1;}

      // make selection
      if ( (selectionKey=spaceChar) | (selectionKey=enterChar) ){let finalSelection = currentSelection;}

      // reprint carrot
      if (~(selectionKey=0)) {
        do Output.moveCursor((currentSelection-1)*distanceBetweenItems+menuY,menuX-1);
        do Output.printChar(carrot);
        do Sys.wait(200);}
      // reprint number of players
      if ( (selectionKey=rightKey) | (selectionKey=leftKey)& (currentSelection=2)) {
        do Output.moveCursor(menuY+distanceBetweenItems,menuX+19);
        do Output.printInt(settings.getNumberOfPlayers());
      }
      // reprint difficulty setting
      if ( (selectionKey=rightKey) | (selectionKey=leftKey)& (currentSelection=3)) {
        do Output.moveCursor(menuY+(distanceBetweenItems*2),menuX+12);
        do Output.printString(difficultySettingNames[difficultySetting]);
      }

      let randomSeed = randomSeed+1;
      if (randomSeed>200){let randomSeed=0;}
      do mathEngine.plantSeed(randomSeed);
    }

    do settings.setUniverseGravity((difficultySetting+1)*90);
    do difficultySettingNames.dispose();

    return;
  }

  method void displayNextTurnTitle(Player player) {
    // displays the "Player X's turn" banner
    var Attributes att;
    var int bannerY, cursorY, playerY, playerX;
    var int i;
    let att=player.getAttributes();

    let playerX = (att.getPx()/settings.getworldResolutionX())+settings.getScreenBoundLeft();
    let playerY = (att.getPy()/settings.getworldResolutionY())+settings.getScreenBoundTop();
    // make sure the banner isnt on top of the player
    if (playerY <120){ let bannerY = 143; let cursorY=13; }
    if (playerY >119){ let bannerY = 77; let cursorY=7; }

    let i=50;
    // make sure the circle won't be drawn outside the Screen
    while ( ((playerX-i)<1) | ((playerX+i)>500) | ((playerY-i)<1) | ((playerY+i)>254) ) {
      let i=i-1;
    }

    // draw the player locator animation
    while (i>settings.getPlayerSpriteSideLength()) {
      // draw the circle outline, and then the objct it might have overwritten;
      do Screen.setColor(true);
      do Screen.drawCircle(playerX, playerY, i);
      do Screen.setColor(false);
      do Screen.drawCircle(playerX, playerY, i-1);
      do drawAllObjects(true);
      do drawScreenEdges();

      // draw banner
      do Screen.setColor(true);
      do Screen.drawRectangle(settings.getBannerLeftX(),bannerY,settings.getBannerRightX(),bannerY+12);
      do Output.moveCursor(cursorY,8);
      do Output.printString(" Player ");
      do Output.printString(player.getName());
      do Output.printString("'s Turn.  Get Ready! ");

      // slow down the animation
      do Sys.wait(50);

      // erase the circle outline
      do Screen.setColor(false);
      do Screen.drawCircle(playerX, playerY, i);
      let i=i-1;
    }
    return;
  }


  method void dispose() {
    // This will destroy this object as well as all objects
    // registered with it.
    var int i;

    // now suicide
    do Memory.deAlloc(this);
    return;
  }

  method AdvancedMath borrowMathEngine() { return mathEngine;}

}
