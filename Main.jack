// Welcome to Gravity Wars!

class Main {

  function void main() {

    var Settings settings;
    var GXEngine gx;
    var PXEngine px;
    var GameEngine myGame;
    var ObjectManager OM;
    var AdvancedMath mx;

    var Player fooPlayer;
    var Attributes fooAtt;

    var int i;
    var String winner;
    var int playerTurn;
    var int foo,bar;

    // load engines
    let settings = Settings.new();
    let mx = AdvancedMath.new();
    let OM = ObjectManager.new(settings);
    let gx = GXEngine.new(settings, mx, OM);
    let px = PXEngine.new(settings, mx, OM);
    let myGame = GameEngine.new(settings, gx, px, mx, OM);

    // menu screen
    do gx.doIntro();

    // loading screen
    do gx.doLoadingScreen();

    // generate a random world
    do myGame.generateUniverse(settings, OM, settings.getUniverseGravity(),
                               settings.getMaxGravitySinkSize(), settings.getNumberOfPlayers());

    // main game loop
    let playerTurn=0;
    while (OM.playersStillAlive()>1)
    {
      let fooPlayer=OM.getPlayer(playerTurn);
      if (~fooPlayer.getIsDead()) {
        do myGame.takeTurn(fooPlayer);
      }
      let playerTurn=playerTurn+1;
      if (playerTurn > (settings.getMaxPlayers()-1)) { let playerTurn=0;}
    }




    let i=0;
    let winner="Nobody";

    let fooPlayer=OM.getPlayer(i);
    while (i<settings.getMaxPlayers() & ~(fooPlayer=0) ) {
      let fooAtt=fooPlayer.getAttributes();
      if (~(fooAtt.getIsDead())) {let winner=fooPlayer.getName();}
      let i =i+1;
      let fooPlayer=OM.getPlayer(i);
    }

    do gx.displayGameResult(winner);


    return;
  }
}
